plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

defaultTasks 'clean', 'build', 'publishToMavenLocal'

group 'sk.baka.slf4j'
version '1.7.26'
ext.local = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
}

apply plugin: 'java'
sourceCompatibility = 1.7
targetCompatibility = 1.7
compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()
}

dependencies {
    compileOnly 'com.google.android:android:4.1.1.4'
    compileOnly 'org.jetbrains:annotations:13.0'
    testCompile 'junit:junit:4.11'
    compile "org.slf4j:slf4j-api:1.7.25"
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'slf4j-handroid'
            version project.version
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

// for Travis to record failures: http://stackoverflow.com/questions/28614865/how-to-read-test-result-reports-on-travis-ci/28617954#28617954
test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'sk.baka'
        name = 'slf4j-handroid'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/slf4j-handroid'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}
